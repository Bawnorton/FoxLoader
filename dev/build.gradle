// Dev build.gradle
plugins {
    id 'groovy'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

group 'com.fox2code'
version project['foxloader.version']

configurations {
    include
    compileOnly.extendsFrom(include)
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()
    api 'org.vineflower:vineflower:1.9.3'
    api 'com.google.code.gson:gson:2.10.1'
    api 'org.semver4j:semver4j:5.2.2'
    api 'org.ow2.asm:asm-commons:9.6'
    api 'org.ow2.asm:asm-util:9.6'
    // Need LWJGL 2 for game decompilation.
    api('org.lwjgl.lwjgl:lwjgl:2.9.3') {
        transitive = false
    }
    include(project(":common")) {
        transitive = false
    }
}

jar.dependsOn(":common:jar")

afterEvaluate {
    jar {
        from(configurations.include.collect { it.isDirectory() ? it : zipTree(it) }) {
            exclude("module-info.class")
        }
    }
}